**Auditors**

[Trust Security](https://twitter.com/trust__90)


---


# Findings

## High Risk
### TRST-H-1 _calculateClaim() does not distribute boost emissions correctly
**Description:**
The function `_calculateClaim()` is responsible for the calculations of the amount of emissions a 
specific veSatin is entitled to claim. The idea is to distribute emissions only to veSatin tokens 
locked for more than **minLockDurationForReward** and only for the extra time the veSatin is 
locked for on top of **minLockDurationForReward**. As an example, if **minLockDurationForReward**
is set to 6 months a veSatin locked for 7 months would receive emissions for 1 month and a 
veSatin locked for 5 months would receive no emissions at all.
To do so the following code is executed in a loop, where every loop calculates the amount of 
emissions the veSatin accumulated during a specific week, in chronological order:
```solidity
      if ((lockEndTime - oldUserPoint.ts) > (minLockDurationForReward)) {
        toDistribute +=
          (balanceOf * tokensPerWeek[weekCursor]) / veSupply[weekCursor];
      weekCursor += WEEK;
      }
```
The code distributes the rewards if the elapsed time between **lockEndTime** (the locking end 
timestamp) and **oldUserPoint.ts** is bigger than **minLockDurationForReward**. However, 
**oldUserPoint.ts** is the timestamp of the last user action on a veSatin, for example depositing LP 
by calling `increaseAmount()`. As an example, a user that locks his veSatin and does nothing 
else will receive rewards for the whole locking duration. In contrast, a user that performs 
one action a week would only receive rewards for the locking duration minus 
**minLockDurationForReward**

**Recommended Mitigation:**
The variable **weekCursor** should be used instead of **oldUserPoint.ts** in the if condition:
```solidity
      if ((lockEndTime - weekCursor) > (minLockDurationForReward)) {
```   
**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, the function `_calculateClaim()` now correctly 
handles emissions for the last minLockDurationForReward seconds of locking time



### TRST-H-2 Users will be unable to claim emissions from veSatin tokens if they withdraw it or merge it
**Description:**
The function `_calculateClaim()` uses the variable **lockEndTime** when checking if a veSatin is 
entitled to emissions for a particular week (code with mitigation from TRST-H-1):
```solidity
           if ((lockEndTime - weekCursor) > (minLockDurationForReward)) {
            toDistribute +=
        (balanceOf * tokensPerWeek[weekCursor]) / veSupply[weekCursor];
        weekCursor += WEEK;
       }
```
However **lockEndTime** is set to 0 whenever a user withdraws a veSatin by calling `withdraw()` or 
merges one by calling `merge()`. When this is the case the operation **lockEndTime - weekCursor** 
underflows, thus reverting. This results in users being unable to claim veSatin emissions if they 
withdraw or merge it first

**Recommended Mitigation:**
In the `withdraw()` and merge() functions, call `claim()` in VeDist.sol to claim emissions before 
setting the lock end timestamp to 0. In `merge()` this is only necessary for the veSatin passed 
as **_from**

**Team Response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, both `merge()` and `withdraw()` now call `claim()` in 
VeDist.sol before **lockEndTime** is set to 0.

### TRST-H-3 Additional Satin/$CASH LP locked in an owner-created veSatin can’t be withdrawn
**Description:**
_The protocol allows the owners to create a special veSatin for the owners and partners via 
`createLockForOwner()`, which does not require to lock LP. On such veSatin, the protocol 
reverts when calling `withdraw()`, `merge()`, and `increaseUnlockTime()`. However the function 
`increaseAmount()`, which is used to lock extra Satin/$CASH LP, does not.
This could lead to partners adding Satin/$CASH LP to their position, but then being unable 
wot withdraw it, since `withdraw()` would revert.

**Recommended Mitigation:**
Revert on calls to `increaseAmount()` for veSatin tokens created via `createLockForOwner()`, 
like in the other functions.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, `increaseAmount()` can’t be called anymore on a 
veSatin created via `createLockForOwner()`.


### TRST-H-4 It’s possible to DOS the distribution of weekly emissions, and as a consequence the whole protocol
**Description:**
The function `updatePeriod()`, responsible for the weekly distribution of emissions, internally 
calls `distributeAll()` on SatinVoter.sol which loops over all of the existing gauges, updates 
them and distribute rewards if necessary. This can be an issue when the number of gauges is 
so high that the execution would cost more gas than the maximum amount permitted in a 
block, thus making the function call to`updatePeriod()` always revert.

**Recommended Mitigation:**
Call distribute() only for the Satin/$CASH LP gauge.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, `updatePeriod()` now only updates and distributes 
emissions to the Satin / $CASH LP gauge.


### TRST-H-5 It’s possible to DOS voting for the whole protocol
**Description:**
The function `_vote()` internally calls `_calculateMaxVotePossible()`, which internally calls 
`getTotalVotingPower()` on Ve.sol, which loops over every veSatin and adds all of their 
current voting power to get the total voting power in the system. This can be an issue when 
the number of veSatin is so high that the execution would cost more gas than the maximum 
amount permitted in a block, thus making `_vote()` always revert.

**Recommended Mitigation:**
It’s possible to leverage the variable **pointHistory** in Ve.sol to get the current total amount of 
voting power in the system. An example of how this is done is in the function 
`_checkpointTotalSupply()` in VeDist.sol.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, `_calculateMaxVotePossible()` now internally 
calculates the total amount of voting power by calling `totalSupply()` on Ve.sol which uses the 
variable **pointHistory**, which does not require unbounded loops.


### TRST-H-6 It’s never possible to vote for new pools until setMaxVotesForPool() is called
**Description:**
The function `_vote()` allows voting on a pool only when the current amount of votes plus the 
new votes is lower or equal to the value returned by `_calculateMaxVotePossible()`:
```solidity
        require(_poolWeights <= _calculateMaxVotePossible(_pool), "Max votes exceeded");
```
However, `_calculateMaxVotePossible()` returns 0 for every pool in which the variable 
**maxVotesForPool** has not been initialized, thus making `_vote()` revert:
```solidity
          return ((totalVotingPower * maxVotesForPool[_pool]) / 100);
```
**Recommended Mitigation:**
In `createGauge()` and `createGauge4Pool()` set **maxVotesForPool** for the pool the gauge is 
being created for to 100.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved by adding an if statement that only executes the requirement if 
**maxVotesForPool[_pool]** is bigger than zero.


### TRST-H-7 The protocol might transfer extra SATIN emissions to veSatin holders potentially making SatinVoter.sol insolvent
**Description:**
The function `_distribute()` in SatinVoter.sol is generally responsible for distributing weekly 
emissions to a gauge based on the percentage of total votes the associated pool received. In 
particular, it’s called by `updatePeriod()` (as per fix TRST-H-4) on the gauge associated with 
the Satin / $CASH pool.
The variable **veShare** is set to be equal to the returned value of 
`calculateSatinCashLPVeShare()`, which is calculated as the percentage of Satin / $CASH LP 
times **claimable[gauge]** and represents the amount of SATIN that will be transferred to 
VeDist.sol when checkpointing emissions in `checkpointEmissions()`:
```solidity
      uint _claimable = claimable[_gauge];
      if (SATIN_CASH_LP_GAUGE == _gauge) {
            veShare = calculateSatinCashLPVeShare(_claimable);
      _claimable -= veShare;
      }
      if (_claimable > IMultiRewardsPool(_gauge).left(token) && _claimable / DURATION > 0) {
          claimable[_gauge] = 0;
      if (is4poolGauge[_gauge]) {
              IGauge(_gauge).notifyRewardAmount(token, _claimable, true);
      } else {
                IGauge(_gauge).notifyRewardAmount(token, _claimable, false);
           }
      emit DistributeReward(msg.sender, _gauge, _claimable);
        }
```
However, when the if condition (**_claimable > IMultiRewardsPool(_gauge).left(token)** **&&** 
**_claimable / DURATION > 0)** is false the variable **claimable[_gauge]** will not be set to 0, 
meaning the next time veShare will be calculated it will include emissions that have already 
been distributed, potentially making SatinVoter.sol insolvent

**Recommended Mitigation:**
Adjust **claimable[gauge]** after calculating **veShare** and calculate **veShare** only if the 
**msg.sender** is SatinMinter.sol to prevent potential attackers from manipulating the value by 
repeatedly calling _distribute():
```solidity
    if (SATIN_CASH_LP_GAUGE == _gauge && msg.sender == minter) {
        veShare = calculateSatinCashLPVeShare(_claimable);
           claimable[_gauge] -= veShare;
              _claimable -= veShare;
        }
``` 
**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, **claimable[gauge]** is now correctly adjusted and 
the **veShare** calculations are executed only when `_distribute()` is called by SatinMinter.sol.



### TRST-H-8 It’s possible to drain all the funds from ExternalBribe
**Description:**
The function `earned()` is used to calculate the amount rewards owed to a **tokenId**, to do so it 
performs a series operations over a loop and then it always executes:
```solidity
    Checkpoint memory cp = checkpoints[tokenId][_endIndex];
      uint _lastEpochStart = _bribeStart(cp.timestamp);
        uint _lastEpochEnd = _lastEpochStart + DURATION;
    if (block.timestamp > _lastEpochEnd) {
            reward += (cp.balanceOf * 
         tokenRewardsPerEpoch[token][_lastEpochStart]) /
           supplyCheckpoints[getPriorSupplyIndex(_lastEpochEnd)].supply;
```

which adds to **reward** the amount of rewards earned by the **tokenId** during the last epoch in 
which it was used to vote, but only if that happened at least a week prior (**block.timestamp** 
**>** **_lastEpochEnd**). Because of this, it’s possible to call `earned()` multiple times in a row with a 
 **tokenId** that voted more than a week before to drain the contract funds.

**Recommended Mitigation:**
The function `earned()` is taken from the Velodrome protocol and is known to have issues. 
Because it uses the convoluted logic of looping over votes to calculate the rewards per
epoch instead of looping over epochs, we recommend using the Velodrome fixed 
implementation, which we reviewed:
```solidity
      function earned(address token, uint tokenId) public view returns (uint) {
      if (numCheckpoints[tokenId] == 0) {
          return 0;
      }
        uint reward = 0;
          uint _ts = 0;
           uint _bal = 0;
             uint _supply = 1;
               uint _index = 0;
          uint _currTs = _bribeStart(lastEarn[token][tokenId]); // take epoch last claimed in as starting point
      _index = getPriorBalanceIndex(tokenId, _currTs);
          _ts = checkpoints[tokenId][_index].timestamp;
              _bal = checkpoints[tokenId][_index].balanceOf;
      // accounts for case where lastEarn is before first checkpoint
        _currTs = Math.max(_currTs, _bribeStart(_ts));
      // get epochs between current epoch and first checkpoint in same epoch as last claim
      uint numEpochs = (_bribeStart(block.timestamp) - _currTs) / DURATION;
      if (numEpochs > 0) {
      for (uint256 i = 0; i < numEpochs; i++) {
      // get index of last checkpoint in this epoch
      _index = getPriorBalanceIndex(tokenId, _currTs + DURATION);
      // get checkpoint in this epoch
      _ts = checkpoints[tokenId][_index].timestamp;
          _bal = checkpoints[tokenId][_index].balanceOf;
      // get supply of last checkpoint in this epoch
            _supply = supplyCheckpoints[getPriorSupplyIndex(_currTs + DURATION)].supply;
      reward += _bal * tokenRewardsPerEpoch[token][_currTs] / _supply;
            _currTs += DURATION;
             }
           }
      return reward;
        }

```
**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, the function `earned()` now uses the suggested 
logic to calculate rewards



### TRST-H-9 Anybody can withdraw the admin fees in the 4pool for themselves
**Description:**
The admin-only function `withdrawAdminFees()` is responsible for collecting the 4pool admin 
fees, the way this is done is by withdrawing the excess amount of tokens in the contract 
relative to the variables tracking the token balances. The function `skim()` does the same 
thing as `withdrawAdminFees()` but it’s a public function, meaning anybody can withdraw the 
admin fees to an arbitrary address.

**Recommended Mitigation:**
Because skim() is supposed to be called by the team and/or a trusted address to collect 
$CASH rebase restricting access to the skim() function only to the team and/or trusted 
addresses solves the issue. 
Important to note that when collecting the $CASH rebase the function withdrawAdminFees() 
should be called first, then the rebase should be distributed, and then `skim()` should be 
followed. If the order is not followed the rebase might be collected as admin fees or the 
admin fees might be collected as rebase.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, a modifier `onlyOwnerOrRebaseHandler()` which 
allows calls only from the owner and the rebase handler contract has been applied to both 
the `withdrawAdminFees()` and `skim()` functions.




## Medium Risk
### TRST-M-1 Division by 0 can freeze emissions claims for veSatin holders
**Description:**
The function `_calculateClaim()` is responsible for the calculations of the amount of emissions a 
specific veSatin is entitled to claim. In doing so, this code is executed (code with mitigation from 
TRST-H-1):
```solidity
    if ((lockEndTime - weekCursor) > (minLockDurationForReward)) {
         toDistribute +=
            (balanceOf * tokensPerWeek[weekCursor]) / veSupply[weekCursor];
    weekCursor += WEEK;
         }
```
The variable **veSupply[weekCursor]** is used as a denominator without checking if it’s 0, 
which could make the function revert. If the protocol ever reaches a state where 
**veSupply[weekCursor]** is 0, all the claims for veSatin that were locked during that week 
would fail for both past and future claims. The same issue is present in the function 
`_calculateEmissionsClaim()`

**Recommended Mitigation:**
Ensure veSupply[weekCursor] is not 0 when performing the division.

**Team Response:**
Fixed

**Mitigation Review:**
The issue has not been resolved in the function `_calculateEmissionsClaim()` but has been 
resolved in the function `_calculateClaim()` which now correctly avoids division by 0.

**Mitigation Review 2:**
The issue has been resolved as suggested in both `_calculateEmissionsClaim()` and 
`_calculateClaim()`, which now correctly avoids division by 0.


### TRST-M-2 Anybody can forfeit partner fees if there are no partners
**Description:**
The protocol accumulates fees for partners whenever a `swap()` happens and there’s at least 
one partner account. The fees can later be claimed for every partner by calling 
`claimPartnerFee()` which distributes the fees to the accounts listed as partners at the 
moment the function is called. If `claimPartnerFee()` is called when there are no partners it zeroes **partnerClaimable0** and **partnerClaimable1** without any distribution occurring, which effectively locks the fees in the contract.

**Recommended Mitigation:**
In `claimPartnerFee()` revert if the number of partners is 0.

**Team Response:**
Fixed

**Mitigation Review:**
The issue has been resolved in a different, but correct, way than the suggested one. The 
function `claimPartnerFee()` still succeeds if called when there are 0 partners, but 
**partnerClaimable0** and **partnerClaimable1** are not zeroed.


### TRST-M-3 BaseV1Pair could break because of overflow
**Description:**
In the function _update(), called internally by `mint()`, `burn()` and `swap()`, the following code 
is executed:
```solidity
    uint256 timeElapsed = blockTimestamp - blockTimestampLast;
     // overflow is desired
    if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
      reserve0CumulativeLast += _reserve0 * timeElapsed;
        reserve1CumulativeLast += _reserve1 * timeElapsed;
     }
```
This is forked from UniswapV2 source code, and it’s meant and known to overflow. It works 
fine if solidity < 0.8.0 is used but reverts when solidity >= 0.8.0 is used.
If this happens all the core functionalities of the pool would break, including `mint()`, `burn()`, 
and `swap()`.

**Recommended Mitigation:**
Wrap the operation around an unchecked{} block so that when the variable overflows it 
loops back to 0 instead of reverting.

**Team Response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, the operation has been wrapped around an 
unchecked{} block


### TRST-M-4 It’s possible to dilute rewards per second in Gauge and Internal bribes
**Description:**
The function `notifyRewardAmount()` is called to distribute new rewards to a gauge or an 
internal bribe. In particular, it increases the duration over which the rewards have to be 
distributed by **DURATION** every time it’s called:
```solidity 
    periodFinish[token] = block.timestamp + DURATION;
```
Because of this an attacker could dilute the rewards per second received by bribes and 
gauges users by calling `notifyRewardAmount()` with a new reward of 1, extending the 
duration of the rewards that are currently being distributed thus lowering the rewards per 
second received by users.

**Recommended mitigation:**
A mitigation that also have positive side-effects in lowering the attack surface is to restrict 
access to the Gauge.sol and InternalBribe.sol `notifyRewardAmount()` functions:
● Adjust `notifyRewardAmount()` in Gauge.sol to be only callable by SatinVoter.sol and
the trusted addresses responsible for sending $CASH rebases, if any.
● Adjust `notifyRewardAmount()` in InternalBribe.sol to be only callable by the 
associated gauge and Ve.sol.

**Team response:**
Fixed

**Mitigation review:**
The issue has been resolved as suggested but a new issue has been introduced, the function 
`claimFees()` in Gauge.sol will fail when calling `notifyRewardAmount()` on InternalBribe.sol 
because SatinVoter.sol does not set the **gauge** parameter in the internal bribe when a new 
gauge is created via `createGauge()`.

**Mitigation review 2:**
The introduced issue has been fixed, now `notifyRewardAmount()` in Gauge.sol is only callable 
by SatinVoter.sol and the $CASH rebase handler while `notifyRewardAmount()` in 
InternalBribe.sol is only callable by Ve.sol and the associated gauge.

### TRST-M-5 createGauge4Pool() lacks proper checks and/or access control
**Description:** 
The function createGauge4Pool() can be called by anybody at any time and is used to create 
a Gauge for a special pool, the 4pool. It takes 5 parameters as inputs: 
```solidity
    function createGauge4pool(
       address _4pool,
          address _dai,
            address _usdc,
               address _usdt,
             address _cash
       ) external returns (address) {
```
None of the parameters are properly sanitized, meaning **_dai, _usdc, _usdt, _cash** could be 
any whitelisted token and not necessarily DAI, USDC, USDT, and cash while **_4pool** could 
be any custom contract, including a malicious one.
The function also sets the variable **FOUR_POOL_GAUGE_ADDRESS** to the newly created gauge, 
overwriting the previous value.


**Recommended Mitigation:**
Make the function only callable by an admin, and if it can be called multiple times, turn the 
variable **FOUR_POOL_GAUGE_ADDRESS** to a mapping from address to boolean to support 
multiple 4 pools.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, `createGauge4Pool()` is now only callable by the 
contract owner and **FOUR_POOL_GAUGE_ADDRESS** has been turned into a mapping, 
**is4PoolGauge**.


### TRST-M-6 _vote() doesn’t check the pool a user is voting for has a valid gauge
**Description:** 
The function `_vote()` allows veSatin holders to vote to which pool the weekly emissions 
should be redirected to but it doesn’t check the pool a user is voting for has a gauge 
associated, which could lead to emissions being lost.

**Recommended Mitigation:**
Ensure that the pool a user is trying to vote for has a valid gauge before voting on it by 
checking if isGauge[pool] it’s true:
```solidity
    if(isGauge[_gauge]) { //<== CHECK IT'S A VALID GAUGE
          int256 _poolWeight = (_weights[i] * _weight) / _totalVoteWeight;
    //
      emit Voted(msg.sender, _tokenId, _poolWeight);
    }
```

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, now the function `_vote()` skips the vote for pools 
that don’t have a valid associated gauge.



### TRST-M-7 It’s possible to freely detach tokens from a gauge
**Description:**
`withdrawToken()` is a public function that takes as an input an **amount** and a **tokenId**. It then 
proceeds to detach the token from the gauge it’s associated with and withdraw the amount. 
A user could pass 0 as an amount with the tokenId he wants to detach, which will withdraw
nothing and detach the token. The function was meant to be called by `withdraw()` internally
which only detaches the token if the full amount deposited is being withdrawn.
In addition to this, `withdrawToken()` doesn’t emit the Withdraw event.

**Recommended mitigation:**
Make `withdrawToken()` internal instead of public.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, now `withdrawToken()` is declared as an internal 
function.

### TRST-M-8 Calculation of the new emissions amount is not 75%/25%/5%
**Description:**
The calculations of the emissions directed to SatinVoter.sol, VeDist.sol, and the treasury are 
done on top of the weekly emissions instead as part of it, resulting in a different percentage 
distribution. The calculations are as follows:
```solidity
    uint _weekly = WEEKLY_EMISSION;
         WEEKLY_EMISSION =(_weekly * _WEEKLY_EMISSION_DECREASE) / _WEEKLY_EMISSION_DECREASE_DENOMINATOR;
    uint _growth = _calculateGrowth(_weekly);
        uint _required = _growth + _weekly;
```
Where **growthDivider**** is set to 20, **toTreasuryDivider** is set to 5 and **_required** is the total 
amount of tokens that will be distributed. Let’s suppose **_weekly** is 1000, **_growth** will be set 
to 50, **_toTeam** to 200, and **_required** to 1250. The distribution would result in 80% to 
SatinVoter.sol, 16% to the treasury, and 4% to VeDist.sol. This results in veSatin holders 
receiving fewer rewards (in relative value) than voters. 

**Recommended mitigation:**
Set **_required** equal to **_weekly** and calculate the amount of emissions to send to 
SatinVoter.sol as the **_weekly** amount minus the emissions sent to treasury and VeDist.sol:
```solidity
        uint _required = _weekly;
    // …snippet…
           token.approve(address(_voter()), _weekly - _growth - _toTeam);
    _voter().notifyRewardAmount(_weekly - _growth - _toTeam);
```
**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, now the calculations of the emissions are done as 
part of the weekly emissions amount instead of on top of it.


### TRST-M-9 Users have to lock Satin/$CASH LP in Ve.sol to vote, but they lose on fees while doing so
**Description:**
To get veSatin tokens and be able to vote users are expected to lock Satin/$CASH LP in 
Ve.sol. When this happens, Ve.sol will start accumulating the fees deriving from the 
Satin/$CASH pool but there is no way for users to collect them, unlike in Gauge.sol. This has 
the effect of having fees locked in Ve.sol and can disincentivize users from locking their LP in 
the system.

**Recommended mitigation:**
Add functionality that allows users to withdraw the fees deriving from the LP they locked 
like it’s done in Gauge.sol. Another option is to have an admin function that can collect the 
fees.

**Team response:**
Fixed

**Mitigation Review:**
The proposed fix adds the function `claimFees()` to Ve.sol, which collects the accumulated 
fees from the Satin/$CASH pool and transfers them to the associated bribe. The fees 
destined to a bribe can only be claimed by users that voted for the pool associated with that 
bribe, the Satin/$CASH pool in this scenario. This means that even if the fees are generated 
by the Satin/$CASH LP locked by every user, they can only be claimed by the subset that 
voted for the Satin/$CASH pool.

**Mitigation Review 2:**
The team acknowledges the issue raised by the mitigation review as intended behavior.


### TRST-M-10 updatePeriod() distributes the same amount of emissions regardless of the time passed from the last distribution
**Description:**
The function `updatePeriod()` distributes new rewards when it’s called more than one week 
after the last distribution. However, if a distribution only happens after two weeks or more, 
the distributed amount is the same as if only one week passed. This can lead to users 
earning fewer rewards than they would expect.

**Recommended mitigation:**
Adjust the amount of emissions to the amount of full weeks passed since the last 
distribution.

**Team response:**
Fixed

**Mitigation Review:**
The proposed fix multiplies the current amount of **WEEKLY_EMISSION** by the number of 
whole weeks (periods) passed since the last **activePeriod**, but the number of emissions 
should decrease by a percentage for every period passed. 
As an example, if 3 periods passed from the last call to `updatePeriod()` and 
**WEEKLY_EMISSION** is set to 1000 SATIN, then the amount of emissions would be calculated 
as 1000 * 3 = 3000 SATIN:
```solidity
        _period = (block.timestamp / _WEEK) * _WEEK;
           uint sinceLast = _period - activePeriod;
         uint emissionsMultiplier = sinceLast / _WEEK;
      uint _weekly = WEEKLY_EMISSION * emissionsMultiplier;
```
However because every period the emissions should decrease by 2% the total amount 
should be 1000 + 1000 * (98/100) + 1000 * (98/100)^2 = 2940 SATIN

**Mitigation Review 2:**
The issue introduced by the fix has been resolved as suggested.



## Low Risk
### TRST-L-1 There is no functionality to remove a whitelisted token

**Description:**
SatinVoter.sol allows to whitelist tokens that can be used as rewards when creating new 
gauges, but there is no functionality that allows removing tokens from the whitelist. This can 
be problematic if some incompatibility and/or exploit is discovered in a whitelisted token.

**Recommended Mitigation:**
Add a function callable only by the owner that allows to remove tokens from the whitelist.

**Team Response:**
Fixed.

**Mitigation Review:**
The issue has been resolved as suggested, there’s now a function `removeWhitelist()` that 
allows removing tokens from the whitelist.


### TRST-L-2 The logic in _calculateClaim() can leave some tokens locked and waste gas
**Description:**
The function `_calculateClaim()` is responsible for the calculations of the amount of emissions a 
specific veSatin is entitled to claim. To do so, this code is executed in a loop for each week from 
the current timestamp to the last claim (code with mitigation from TRST-H-1):
```solidity
    if ((lockEndTime - weekCursor) > (minLockDurationForReward)) {
             toDistribute +=
         (balanceOf * tokensPerWeek[weekCursor]) / veSupply[weekCursor];
      weekCursor += WEEK;
    }
```
When the if condition is not met two things happen:
- An amount of emissions that was supposed to be distributed ((balanceOf * 
tokensPerWeek[weekCursor]) / veSupply[weekCursor])) is skipped, meaning it will stay 
locked in the contract.
- The function `_calculateClaim()` will loop for the maximum number of times (50), because 
**weekCursor** is not increased, wasting users' gas.

**Recommended Mitigation:**
When the if condition is not met burn the tokens that were supposed to be distributed and 
exit the loop. Since the non-distributed tokens would stay locked it’s not strictly necessary to 
burn them.

**Team Response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, the function `_calculateClaim()` exits the loop as soon 
as necessary. The non-distributed tokens stay locked in the contract



### TRST-L-3 It’s possible to extend the lock of an expired veSatin
**Description:**
The function `merge()`, which is used to merge two different veSatin into one, doesn’t ensure 
that the veSatin that should be merged is not expired. This allows a user to extend a veSatin 
lock time even after it expires, something the protocol assumes not to be possible. This can 
brick emissions claims and is an extra tool an attacker can use

**Recommended Mitigation:**
In the `merge()` function require that both of the veSatin used for the merge are not expired.

**Team response:**
Fixed

**Mitigation Review:**
The issue persists because the proposed fix only checks that the veSatin with the latest 
expiry is not expired:
```solidity
    uint end = _locked0.end >= _locked1.end ? _locked0.end : 
          _locked1.end;
       require(end > block.timestamp, "expired");
``` 
An effective solution needs to ensure that both _locked0.end and _locked1.end are not 
expired:
```solidity
       uint end = _locked0.end >= _locked1.end ? _locked0.end : 
           _locked1.end;
      require(_locked0.end > block.timestamp && _locked1.end > 
  block.timestamp, "expired");
```
**Mitigation Review 2:**
The issue has been resolved as suggested


### TRST-L-4 It’s possible to artificially increase the supply variable in Ve.sol
**Description:**
The function `merge()`, which is used to merge two different veSatin into one, internally calls 
`_depositFor()` which as part of its execution increases the variable **supply**, responsible for 
tracking the amount of Satin/$CASH LP locked. Since when merging two veSatin there is 
never an increase in locked tokens in the system, the function `merge()` should adjust **supply**
back, but this never happens. This bug can be used to artificially increase the variable **supply** 
by merging veSatin over and over again.

**Recommended mitigation:**
In `merge()` remove **value0**, the amount of Satin/$CASH LP locked in the first veSatin, from 
**supply**:
```solidity
      supply -= value0;
``` 
**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, supply is now correctly adjusted.


### TRST-L-5 _notifyRewardAmount() is incompatible with fee on transfer tokens
**Description:** 
The function _notifyRewardAmount() transfers an **amount** of a **token** specified as input to 
itself:
```solidity
           IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
            rewardRate[token] = (amount * PRECISION) / DURATION;
```
After this, it sets the variable **rewardRate[token]** assuming the amount of token received is 
equal to the **amount** it transferred, which is not true in case of tokens that have a fee on 
transfers. This leads to the first users calling `getReward()` to withdraw more tokens than they 
should, and the last user(s) not being able to withdraw at all since the balance is lower than 
expected.

**Recommended mitigation:**
Since only whitelisted tokens are allowed to be used as rewards, the best mitigation is to 
make sure fees on transfer tokens are not whitelisted.

**Team response:**
Added an additional mitigation layer.

**Mitigation Review:**
The team implemented an additional mitigation layer in the function `registerRewardToken()`, 
it’s now only callable by the owner while previously being callable also by users with a 
certain amount of voting power.


### TRST-L-6 Modifier onlyNewEpoch() does not ensure new epoch is reached
**Description:** 
The modifier `onlyNewEpoch()` ensures at least **DURATION** seconds passed since the last 
voting action, but does not ensure it’s a different epoch:
```solidity
    modifier onlyNewEpoch(uint _tokenId) {
        require((block.timestamp / DURATION) * DURATION > 
          lastVoted[_tokenId], "TOKEN_ALREADY_VOTED_THIS_EPOCH");
```
This can result in users being able to execute two voting actions in a single epoch, something 
the protocol is trying to avoid.

**Recommended mitigation:**
In both `vote()` and `reset()` set **lastVoted** to the current epoch timestamp, which can be 
obtained from the **activePeriod** variable in SatinMinter.sol, and in the `onlyNewEpoch()` 
modifier require the current **activePeriod** to be strictly greater than **lastVoted**.

**Team response:**
Fixed

**Mitigation Review:**
The issue has been resolved as suggested, the modifier `onlyNewEpoch()` now requires the 
current activePeriod to be strictly greater than lastVoted, and lastVoted is set to the current 
activePeriod when either `vote()` or `reset()` is called.

### TRST-L-7 _derivedBalance() doesn’t properly apply boosted rewards based on voting power
**Description:** 
The function `_derivedBalance()` is used to adjust the balance of a pool voter based on his 
percentage of voting power and LP deposited. However the adjustment is only applied to 
the Satin / $CASH LP gauge because the function returns earlier in any other scenario: 
```solidity
    if (underlying != IVe(ve).token()) {
        return _derived;
    }
```
This means the emissions received by a voter are not increased based on the total voting 
power they have in the system, with the exception of the Satin / $CASH LP, which is counter intuitive because users have to either lock Satin / $CASH LP in Ve.sol to get voting power or 
in the gauge to get emissions.

**Recommended mitigation:**
The team likely intended to have the opposite effect, not increasing the emissions based on 
voting power for the Satin / $CASH LP but increasing it for every other gauge:
```solidity
    if (underlying == IVe(ve).token()) { //⇐ IF EQUAL
          return _derived;
    }
```
**Team response:**
The team disputed and is aware of the issue which is considered intended behavior.


### TRST-L-8 The owner can acquire an arbitrary amount of voting power
**Description:** 
The function `createLockForOwner()` in Ve.sol allows the owner of the protocol to mint a 
veSatin without requiring Satin/$CASH LP to be deposited. This allows the owner to get an 
arbitrary amount of voting power in the system.

### TRST-L-9 The owner can mint an arbitrary amount of Satin
**Description:** 
The function `ownerMint()` allows the owner to mint an arbitrary amount of Satin. If the 
owner is compromised this can make the protocol collapse instantly. Consider limiting the 
power of the owner account.

**Team Response:**
"Both capabilities will be walled behind a 5/9 ecosystem multisig, with 36 hour timelock."



## Informational
### Initialize ReentrancyGuardUpgradable
Both Ve.sol and SatinVoter.sol import ReentrancyGuardUpgradable.sol, but in both 
instances it’s not initialized in the `initialize()` function. It is not strictly necessary for the guard 
functionality, but still a good practice.

### More comprehensive testing
Some issues, like TRST-H-1 and TRST-H-2, could have been caught earlier in the development 
process with a more robust test suite. Consider writing extensive tests for the changes made 
from the initial codebase.