**Auditors**

[Zokyo](https://x.com/zokyo_io)

# Findings

## High Risk

### Lack of an implementation for withdrawing beneficiaries balance

**Severity**: Critical

**Status**: Resolved

**Location**: BandoFulfillmentManagerV1.sol

**Description**

The beneficiaryWithdraw() function in both BandoERC20FulfillableV1 and BandoFulfillableV1 contracts are only designed to be called by the manager contract but the BandoFulfillmentManagerV1 contract is missing an implementation for withdrawing the beneficiaries available balances.
```solidity
function beneficiaryWithdraw(uint256 serviceID, address token) public virtual nonReentrant {
        require(_manager == msg.sender, "Caller is not the manager");
        ...
}
```

**Recommendation**: 

Add a function to withdraw beneficiaries available balances in the BandoFulfillmentManagerV1 contract.

### Fund locked due to lack of an implementation for withdrawing service fees

**Severity**: Critical

**Status**: Resolved

**Location**: BandoRouterV1.sol

**Description**

In the BandoRouterV1 contract, the requestService() function requires the caller to pay the sum of request.weiAmount and service.feeAmount.
```solidity
(bool success, uint256 total_amount) = request.weiAmount.tryAdd(service.feeAmount);
if (!success) {
    revert OverflowError();
}
       
if (msg.value != total_amount) {
    revert AmountMismatch();
}
```

But the function calls the _escrow.deposit() function with only request.weiAmount and service.feeAmount is locked in the contract.
```solidity
IBandoFulfillable(_escrow).deposit{value: request.weiAmount}(serviceID, request);
```

There is no implementation to withdraw this service fee in the contract and the fees are locked forever.

**Recommendation**: 

Add a function to withdraw the service fees locked in the contract.

### Loss of fulfiller funds and fees due to wrong comparison between depositsAmount and total_amount

**Severity**: Critical

**Status**: Resolved

**Location**: BandoERC20FulfillableV1.sol

**Walkthrough**:
Say a user will like to submit a request for a ERC20Service.

**Initial states:**

```solidity
function getERC20DepositsFor(address token, address payer, uint256 serviceID) public view returns (uint256 amount) {
    amount = _erc20_deposits[serviceID][token][payer];
}
```

The initial deposit for the user is 0.

So when the user calls the router contract:
```solidity
function requestERC20Service(
    uint256 serviceID, 
    ERC20FulFillmentRequest memory request
) public payable whenNotPaused nonReentrant returns (bool)
```
 
The user transfers in request.tokenAmount, and thus deposits request.tokenAmount in the BandoERC20FulfillableV1.sol contract.
```solidity
IERC20(request.token).safeTransferFrom(
     msg.sender,
    _erc20Escrow,
    request.tokenAmount
);
require(
    IERC20(request.token).balanceOf(msg.sender) <= pre_balance - request.tokenAmount,
    "BandoRouterV1: ERC20 invalid transfer return"
);
IBandoERC20Fulfillable(_erc20Escrow).depositERC20(serviceID, request);
```

In the depoistERC20 function in the BandoERC20Fulfillable contract, amount is set as 

```solidity
uint256 amount = fulfillmentRequest.tokenAmount;
```

Since users current Deposit is 0

When setting their new deposit amount, their deposit is result, which is (0 + request.tokenAmount =  request.tokenAmount)
```solidity
(bool success, uint256 result) = amount.tryAdd(depositsAmount);
require(success, "Overflow while adding deposits");
setERC20DepositsFor(
    token,
    fulfillmentRequest.payer,
    serviceID,
    result
);
```

So the user has request.tokenAmount in their deposit balance for the service ID.

Also
```solidity
ERC20FulFillmentRecord memory fulfillmentRecord = ERC20FulFillmentRecord({
    id: _fulfillmentIdCount,
    serviceRef: fulfillmentRequest.serviceRef,
    externalID: "",
    fulfiller: service.fulfiller,
    entryTime: block.timestamp,
    payer: fulfillmentRequest.payer,
    tokenAmount: fulfillmentRequest.tokenAmount,
    feeAmount: service.feeAmount,
    fiatAmount: fulfillmentRequest.fiatAmount,
    receiptURI: "",
    status: FulFillmentResultState.PENDING,
    token: fulfillmentRequest.token
});


_fulfillmentRecords[fulfillmentRecord.id] = fulfillmentRecord;

 
_fulfilmentRecords[fulfillment.id].tokenAmount = request.tokenAmount as seen above
```
Now when it's time to fulfill:
```solidity
function registerFulfillment(uint256 serviceID, FulFillmentResult memory fulfillment) public virtual nonReentrant returns (bool) {
…
uint depositsAmount = getERC20DepositsFor(
    token,
    _fulfillmentRecords[fulfillment.id].payer,
    serviceID
);
(bool ffsuccess, uint256 total_amount) = _fulfillmentRecords[fulfillment.id].tokenAmount.tryAdd(
    service.feeAmount
);
require(ffsuccess, "Overflow while adding fulfillment amount and fee");
require(depositsAmount >= total_amount, "There is not enough balance to be released");

```

So the check:

```solidity
require(depositsAmount >= total_amount, "There is not enough balance to be released");
```

Is actually checking:
```solidity
require(request.tokenAmount >= request.tokenAmount + service.feeAmount, "There is not enough balance to be released");
```

Which is always false when there is a service fee, causing the whole transaction to fail.

This same issue for requesting a service using native tokens.

**Recommendation**: 

Service fees need to be properly accounted for when the user deposits and not when the fulfiller fulfills to avoid transaction failure after the fullfiler does their task.

### Lack of an implementation for taking service fees in ERC20 service requests

**Severity**: High

**Status**: Resolved

**Location**: BandoRouterV1.sol

**Description**

The requestERC20Service() function is missing to take the service fee by transferring only request.tokenAmount from the caller even if the requestService() function has a check to make sure that msg.value is equal to the sum of request.weiAmount and service.feeAmount.

**Recommendation**: 

Calculate the total amount of the request.tokenAmount and service.feeAmount and transfers the total amount from the caller.

## Medium Risk

### There is no reason for requestERC20Service to be payable - can lead to fund loss

**Severity**: Medium

**Status**: Resolved

**Location**: BandoRouterV1.sol

**Description**

In the current implementation, the requestERC20Service() function is payable, which means that users can send ETH along with the transaction call, however the functions has no operations to do with ETH, meaning that if a user accidentally sends ETH along with their function call, the ETH will be trapped in the contract which leads to fund loss for the user.

**Recommendation**: 

Remove payable keyword.

### Lack of updating `_serviceCount` variable

**Severity**: Medium

**Status**: Resolved

**Location**: FulfillableRegistry.sol

**Description**

The addService() function is missing to update the _serviceCount variable.
Due to a lack of increasing the variable, the removeServiceAddress() function will always fail.

**Recommendation**:

Update the _serviceCount variable in the function.

### High slippage between ETH based requests between request and fulfillment

**Severity**: Medium

**Status**: Acknowledged

**Description**

ETH is a very volatile asset and there is a concern if an asset like ETH is compatible with what the project is trying to achieve. A high level overview of the protocol is that a user can pay in crypto on-chain for off chain services to be fulfilled. Consider this scenario, a user wants to buy a $100 gift card in eth, so they submit a request paying 11,235 GWEI (approximately $100 at the time of writing). 

Scenario 1) Consider if the price of ETH rises by 10% from the time of the request and before the fulfiller fulfills the transaction. This will mean the user has now paid $110 worth of eth for a $100 gift card, and since there is no partial refund, the fulfiller profits and extra $10

Scenario 2) Consider if the price of ETH falls by 10% from the time of the request and before the fulfiller fulfills the transaction. This will mean that the user has paid $90 worth of eth for a $100 gift card. Unless the fulfiller wants to lose money, the transaction will not be executed and a cancellation will likely be forced.

**Recommendation**: 

It is probably safer and more consistent to restrict this protocol to handle stable coins only. A suggestion would also to be to implement ERC-2612 Permit to extend functionality of your contract to allow gasless payments
https://eips.ethereum.org/EIPS/eip-2612

**Client comment**: 

Although it doesnt actually completely solve the issue. We introduced a "swap fee" for volatile assets to mitigate slippage and volatility. As we find it paramount for our business to support a wide range of tokens and assets to give them more utility.
https://github.com/bandohq/evm-fulfillment-protocol/pull/76



### Fulfilments are based on variable service fee

**Severity**: Medium

**Status**: Resolved

**Description**

In the current implementation of the protocol, when registering a fulfillment, the service fee is fetched from the registry contract.
Service memory service = _registryContract.getService(serviceID);


The problem is that during any time of the protocols operation the owner can change the service fee.
```solidity
function updateServiceFeeAmount(uint256 serviceId, uint256 newFeeAmount) external onlyOwner {
    require(_serviceRegistry[serviceId].fulfiller != address(0), 'FulfillableRegistry: Service does not exist');
    _serviceRegistry[serviceId].feeAmount = newFeeAmount;
}
```

The protocol is designed to handle many requests. If the service fee is changed between a user's request and the fulfillment, this can cause the fulfillment transaction to fail as the accounting is off and the user has not sent the fee amount that corresponds to the new fee. E.g if a user initially requested when the fees were 1 and a fee change to 2 occurs before the request is fulfilled, the fulfillment transaction will fail due to the user not having not sent enough funds to cover the new fee.

**Recommendation**: 

Cache the fee that the user requested on at the time of the request, so even if there is a fee change before the user's request is fulfilled, they will still be charged based on the fee they submitted at the time of request.

### Users can't withdraw their own refunds themselves 

**Severity**: Medium

**Status**: Resolved

**Location**: BandoERC20FulfillableV1.sol, BandoFulfillableV1.sol


**Description**

In the current implementation, if a user has a pending refund, they have to wait for the service fulfiler or the owner to process the refund.
```solidity
function withdrawERC20Refund(uint256 serviceID, address token, address refundee) public virtual nonReentrant returns (bool) {
    require(_manager == msg.sender, "Caller is not the manager");
    ...
}
```


This is an unusual pattern for a number of reasons
At periods of high network traffic, it will take long for a single entity to process refunds for all the users
Processing refunds costs gas for the fulfiler so the cost of processing a refund can cause financial loss as they are a) paying gas to fulfill the transaction b) paying gas again to submit the refund
If the fulfiler or owner ever goes offline, users refunds get stuck, violating the decentralized nature of the protocol
The users refund is already made available to them through the mapping:
If the refunds are already authorized, there is no harm with letting a user withdraw their own refunds

**Recommendation**: 

If user refunds are already authorized, let them be free to withdraw it when they want. This will save gas for the protocol and time for the user, while mitigating risk of funds being unduly trapped 

### payer is not tied to msg.sender

**Severity**: Medium

**Status**: Resolved

**Description**

Bando fulfillment requests have the field “payer”. This field is user imputed and does not necessarily need to reflect the person making the transaction. This means a malicious user can create spam fake transactions on behalf of another user, which the fulfiler will eventually need to refund. At the minimum this leads to fulfiler loss as they have to spend gas submitting the fulfillment transactions and submitting refunds.

Out of scope thoughts: It is unclear how the protocol punishes spam, however if the fulfiler sees spam from a specific payer they might not be included to process their transactions. But if an attacker is impersonating a regular user, this penalty will be on a regular user.

**Recommendation**: 

In the router contract ensure request.payer == msg.sender. If you want to allow 3rd parties to submit transactions on behalf of other users, you may consider implementing signed transactions to your protocol.

### `disableInitializers` not used to prevent uninitialized contracts

**Severity**: Medium	

**Status**: Resolved

**Description**

All the contracts are designed to be upgradeable.

In order to prevent leaving an implementation contract uninitialized it is recommended adding the _disableInitializers() function in the constructor to lock the contracts automatically when they
are deployed.

**Recommendation**:

Add `_disableInitializers()` to the constructor.


### The removeServiceAddress() function fails with Overflow error

**Severity**: Medium	

**Status**: Resolved

**Description**

In FulfillableRegistry.sol,the removeServiceAddress() function  fails with an Underflow error. This is because _serviceCount is never incremented when adding the service. 

**Recommendation**:

Please consider incrementing the _serviceCount when adding a new service in the function such as the example given below

```solidity
   function addService(uint256 serviceId, Service memory service) external onlyManager returns (bool) {
       require(
           _serviceRegistry[serviceId].fulfiller == address(0),
           'FulfillableRegistry: Service already exists'
       );
       _serviceRegistry[serviceId] = service;
       // @audit Line below Added by auditor to test 
       _serviceCount++;
       emit ServiceAdded(serviceId, service.fulfiller);
       return true;
   }
```





## Low Risk

### Lack of emitting events

**Severity**: Low

**Status**: Resolved

**Location**: BandoERC20FulfillableV1.sol, BandoFulfillableV1.sol, BandoFulfillmentManagerV1.sol, BandoRouterV1.sol, FulfillableRegistry.sol


**Description**

setManager(), setRouter(), setFulfillableRegistry() functions in the BandoERC20FulfillableV1 and BandoFulfillableV1 contracts.
setServiceRegistry(), setEscrow(), setERC20Escrow(), setService(), setServiceRef() functions in the BandoFulfillmentManagerV1 contract.
setFulfillableRegistry(), setTokenRegistry(), setEscrow(), setERC20Escrow() functions in the BandoRouterV1 contract.
setManager(), updateServiceBeneficiary(), updateServiceFeeAmount(), updateServiceFulfiller(), addFulfiller(), addServiceRef() functions in the FulfillableRegistry contract.
The above functions are missing events when key storages are updated.

**Recommendation**: 

Add relative events in the functions.

## Informational

### Floating pragma

**Severity**: Informational

**Status**: Resolved

**Location**: All contracts

**Description**

The contracts should not use floating pragma, e.g. (pragma solidity >=0.8.20 <0.9.0;), which allows a range of compiler versions. It is important to lock the pragma to prevent contracts from being accidentally deployed using a compiler with unfixed bugs.

**Recommendation**: 

Fix the pragma version to a single version to ensure consistent behavior.

### Accidentally setting fulfiler address to 0 has effect of disabling service

**Severity**: Informational

**Status**: Resolved

**Location**: FulfillableRegistry.sol

**Description**

The FulfillableRegistry has lots of checks that revert if the fulfiller is the zero address.

There is a way to accidentally disable a service by setting the fulfiller to zero after the service fields have been set.
```solidity
function updateServiceFulfiller(uint256 serviceId, address newFulfiller) external onlyOwner {
    require(_serviceRegistry[serviceId].fulfiller != address(0), 'FulfillableRegistry: Service does not exist');
    _serviceRegistry[serviceId].fulfiller = newFulfiller;
}
```

There are no checks that the newFulfiller is not the 0 address.

**Recommendation**: 

Add a check that verifies the newFulfiller address is not the 0 address.

### Pre and post balance checks on msg.sender is not needed

**Severity**: Informational

**Status**: Resolved

**Location**: BandoRouterV1.sol

**Description**

In the requestERC20Service() function, the additional safety checks on the pre and post balance of msg.sender are not needed, and a gas spending external calls.
```solidity
require(pre_balance >= request.tokenAmount, "BandoRouterV1: Insufficient balance");
    IERC20(request.token).safeTransferFrom(
    msg.sender,
    _erc20Escrow,
    request.tokenAmount
);
require(
    IERC20(request.token).balanceOf(msg.sender) <= pre_balance - request.tokenAmount,
    "BandoRouterV1: ERC20 invalid transfer return"
);
```

The safeTransferFrom function already has built-in safety checks ensuring that the function will revert if the user doesn't have enough balance, this is also true for tokens that return a bool instead of reverting.

**Recommendation**: 

Removing the pre and post balance checks allows gas to be saved by avoiding unnecessary computation and external calls.

There is another pattern you could use if you wanted to account for fee-on-transfer tokens, but these types of tokens don't seem compatible with the protocol and should not be whitelisted.

### Consider using custom errors instead of strings

**Severity**: Informational

**Status**: Resolved

**Location**: BandofulfillableV1.sol

**Description**

Using custom errors is more gas-efficient than reverting with strings because they reduce storage and execution costs by not storing string literals. Custom errors also improve code readability by providing more structured and specific failure conditions.

**Recommendation**: 

Consider using custom errors to save gas.
(https://soliditylang.org/blog/2021/04/21/custom-errors/)

### Unnecessary check

**Severity**: Informational

**Status**: Resolved

**Location**: BandoFulfillableV1.sol, BandoERC20FulfillableV1.sol

**Description**

In the _authorizeRefund() function, the deposits variable is checked with both weiAmount and total_refunds.
However, the total_refunds is the sum of weiAmount and authorized_refunds and its value is always equal or greater than weiAmount.
So the comparison between “deposits” and “weiAmount” is not necessary because the “deposits” is compared with “total_refunds” again and it’s only passed when “deposits >= total_refunds”.
“deposits >= total_refunds” means that “deposits >= weiAmount”.

**Recommendation**: 

Remove the comparison between “deposits” and “weiAmount”.

### Duplicated nonReentrant modifier calls

**Severity**: Informational

**Status**: Resolved

**Location**: BandoFulfillmentManagerV1.sol, BandoRouterV1.sol


**Description**

In the BandoFulfillmentManagerV1 contract, the registerFulfillment(), withdrawERC20Refund(), and registerERC20Fulfillment() functions have the nonReentrant modifiers and the functions make the external calls to the _escrow and _erc20_escrow contracts.
However, the external calls also have the nonReentrant modifiers so the entire transactions will have duplicated nonReentrant modifier calls.
The same for the requestERC20Service() and requestService() functions in the BandoRouterV1 contract.

**Recommendation**: 

Remove nonReentrant modifiers in the functions of the BandoFulfillmentManagerV1 and BandoRouterV1 contracts.

