**Auditors**

[Zokyo](https://x.com/zokyo_io)

# Findings

## High Risk

###  Out of range index in `getTokenBurnFee()` causes function reversion after 14 days

**Severity**: Critical

**Status**: Resolved

**Location**: PoolBase.sol

**Description**

The `getTokenBurnFee()` function in `PoolBase.sol` attempts to return `burnFees[feeCycle.length]` after 14 days of asset locking, which is out of range for the burnFees array. This out-of-bounds access results in the function reverting, causing subsequent `unstake` operations to fail and denying users access to their assets after the maturity period has passed.

```solidity
920 function getTokenBurnFee(address _staker) public view returns (uint256) {
921     User memory user = users[_staker];
922     for (uint256 i; i < feeCycle.length; i++) {
923     if (
924         (block.timestamp < user.lastUnstakedTimestamp + feeCycle[i]) ||
925         block.timestamp < user.lastInvestTimestamp + feeCycle[i]
926     ) {
927         return burnFees[i];
928     }
929     }
930     return burnFees[feeCycle.length];
931 }
```

**Recommendation**: 

To resolve this issue, the function should be modified to handle cases where the index feeCycle.length is out of range more gracefully. Instead of accessing burnFees[feeCycle.length] directly, consider returning a default burn fee value, or change the index to feeCycle.length-1.  

**Fix**:  

Issue fixed at commit a9767ff4

### Reentrance vulnerability in `processRewards()` and `updateStakeLock()` Functions

**Severity**: High

**Status**: Resolved

**Location**: PoolBase.sol

**Description**

The `processRewards()` and `updateStakeLock()` functions in the `PoolBase.sol` contract are exposed to reentrancy attacks. This issue surfaced after the client attempted to fix the `_processVaultRewards()` function without adjusting its side effects in other parts of the codebase. As a result, malicious actors could exploit this vulnerability to manipulate the contract state and potentially drain funds or alter stakes.

**Recommendation**: 

Integrate a reentrancy guard: Utilize a reentrancy guard, such as the `nonReentrant` modifier provided by OpenZeppelin that is already imported in the project, to prevent reentrant calls to vulnerable functions. This will effectively lock the contract during execution and prevent reentrancy attacks.

**Fix**:  

Issue fixed at commit a9767ff4

### Attacker Can Grief A Victim Creating Many Little Deposits On Victim’s Behalf 

**Severity** - High

**Status** - Resolved

**Description**

An attacker can create many small deposits on behalf of the victim using just dust amount (using the `stakeFor()` function) , with each subsequent stake a new deposit struct would be pushed to `user.deposits` (L563) , the gas cost of staking for the victim would be much cheaper in an L2 . 
Therefore , now when the victim unstakes and `_unstake` is invoked , it calls the `_processRewards` which in turn calls `getRewardLockPeriod()` which executes a for loop →
```solidity
for (i = 0; i < user.deposits.length; i++) {
      Deposit storage stakeDeposit = user.deposits[i];
      if (!stakeDeposit.isYield) {
        totalSum =
          totalSum +
          stakeDeposit.tokenAmount *
          (stakeDeposit.lockedUntil - stakeDeposit.lockedFrom);
      }
    }
```


Now since the user.deposits array is way bigger than it should be the for loop would consume lots of gas (or eventually revert) for the victim.

**Recommendation**:

Have a minimum stake amount which makes the griefing non-profitable

### Reentrancy Guard Conflict in `_processVaultRewards` Private Function

**Severity**: High

**Status**: Resolved

**Location**: CorePool.sol

**Description**

The private function `_processVaultRewards` in `CorePool.sol` is equipped with a `nonReentrant` modifier. This function is called by both internal and external functions that also use the `nonReentrant` modifier, causing a reentrancy guard conflict. Consequently, the function becomes non-executable due to the reentrancy lock it initiates. Adjusting the reentrancy guards or applying a different guard for `_processVaultRewards` is necessary to resolve this issue. This issue potentially leads to loss of funds.

**Recommendation**: 

To resolve the reentrancy guard conflict in `_processVaultRewards`, you can consider the following recommendations:

- **Refactor the Callers**: Ensure that functions calling `_processVaultRewards` manage the reentrancy guard properly. This might involve only applying `nonReentrant` to external or public functions and ensuring that internal functions like `_processVaultRewards` are called in a guarded context.
- **Use Separate Reentrancy Guards**: If necessary, use a different reentrancy guard for `_processVaultRewards` to differentiate it from the guards used by its callers. This can be implemented by creating a new `ReentrancyGuard` instance or using a custom reentrancy mechanism for this function.

By implementing these changes, you can ensure that `_processVaultRewards` executes correctly without encountering reentrancy guard conflicts.

## Medium Risk

### `vaultRewardsPerWeight` Should Be Updated When `_processVaultRewards()`

**Severity** - Medium

**Status** - Resolved

**Description**

The `vaultRewardsPerWeight`  is incremented whenever there are rewards transferred in to the core pool contract →

```solidity
SafeERC20.safeTransferFrom(IERC20(buidl), msg.sender, address(this), _rewardsAmount);


    vaultRewardsPerWeight += rewardToWeight(_rewardsAmount, usersLockingWeight);
```

But it is not decremented when `BUIDL` is transferred out of the contract in `_processVaultRewards()` →

```solidity
// transfer fails if pool BUIDL balance is not enough - which is a desired behavior
    SafeERC20.safeTransfer(IERC20(buidl), _staker, pendingVaultClaim);
```

**Recommendation**:

Update the `vaultRewardsPerWeight` inside `_processVaultRewards()`.

### Rounding Issue While Calculating `liquidPercentage`

**Severity** - Medium

**Status** - Resolved

**Description**

The `liquidPercentage` calculated inside `_processRewards()` (L773) is as follows →
```solidity
Deposit memory newDeposit = Deposit({
        tokenAmount: pendingYield,
        lockedFrom: uint64(block.timestamp),
        lockedUntil: uint64(block.timestamp + getRewardLockPeriod(_staker)), // staking yield for Reward Lock Period
        weight: depositWeight,
        liquidPercentage: (user.liquidWeight * 100) / user.totalWeight, //AUDIT-round
        isYield: true
      });
```

Here a new deposit is being created (for rewards) and the `liquidPercentage` is `user.liquidWeight*100 / user.totalWeight` , if the `totalWeight` of the user is > `100*liquidWeight` (quite possible if the user has staked a very little amount as liquid and rest non-liquid) then the liquidPercentage would round to 0. Therefore, a user with `liquidWeight > 0` will receive 0 yield for his deposit due to the rounding.

**Recommendation**:

Use higher precision for calculating `liquidPercentage`.

### Staker Can Extend Expired Locks

**Severity** - Medium

**Status** - Resolved

**Description**

When a user stakes , his stake gets assigned a weight which is dependent on the length of the stake and the amount staked. A user can also update his stake which updates the deadline (`lockedUntil`) of his stake , so if the stake was previously 10 days long a user can extend that to 12 days.

A user might have a 5 day long stake which would end on day 5 , assuming he wants to update the stake at day 8 (3 days after the stake ended) the new weight of the stake would also account for the 3 days in between i.e. new weight will be calculated as (`_updateStakeLock()`) →
```solidity
int256 newWeight = (((stakeDeposit.lockedUntil - stakeDeposit.lockedFrom) *
      WEIGHT_MULTIPLIER) /
      365 days +
      WEIGHT_MULTIPLIER) * stakeDeposit.tokenAmount;
```


The `newWeight` calculated will account for the entire `8 + new deadline` days which includes the 3 days where the stake was not active and therefore rewards would be accounted for the entire 8 days (till the new deadline) in `_processRewards()`


**Recommendation**:

Update the logic to only account weight and rewards when the stake was active.

## Low Risk

### Missing Deposit ID Emission on New Deposit

**Severity**: Low

**Status**: Resolved

**Location**: PoolBase.sol

**Description**

When a new deposit is pushed to the deposits array using `user.deposits.push(deposit);`, the `depositId`, which corresponds to the index of this new deposit, is not emitted. This ID is necessary for subsequent functions like `_unstake()` and `_updateStakeLock()`, as it refers to the specific position of the deposit in the deposits array. The absence of emitting this `depositId` can lead to difficulty in tracking and managing deposits efficiently.

**Recommendation**: 

To address this issue, it is recommended to emit an event whenever a new deposit is pushed to the `deposits` array. This event should include the `depositId` so that users can easily reference the index of their deposits for future operations like unstaking or updating stake locks. The implementation might look like this:
```solidity
event DepositMade(address indexed user, uint256 depositId, uint256 amount);

function deposit(uint256 _amount) external {
    // Assume necessary validations and logic here
    
    uint256 depositId = user.deposits.length;
    user.deposits.push(deposit);
    
    emit DepositMade(msg.sender, depositId, _amount);
    
    // Continue with other logic if necessary
}
```

By emitting this event, users will have a better way to track their deposits and the corresponding IDs, improving overall usability and manageability.

**Fix** - Issue addressed and fixed in commit e935e74   by adding `emit Deposited(_staker, user.deposits.length - 1, pendingYield);` whenever `user.deposits` is pushed onto.


### Inadequate Input Validation in `setStakingConfig` Function

**Severity**: Low

**Status**: Resolved

**Location**: PoolBase.sol

**Description**

The `setStakingConfig` function in the `PoolBase.sol` contract lacks proper input validation for the `_index` parameter. The `_index` parameter is used to update the values of `feeCycle` and `burnFees` arrays. Given that `feeCycle` is of length 4 and `burnFees` is of length 5, inadequate checks on the `_index` value can lead to out-of-bounds errors or unexpected behavior. This discrepancy in array lengths and the absence of checks for valid `_index` values could result in setting configurations that are not intended or are erroneous. It might as well be necessary to validate that `minStakeTime`, `_cycle` and `_fee` are within acceptable bounds.

**Recommendation**: 

Add appropriate input validation for the `_index` parameter within the `setStakingConfig` function. The validation should ensure `_index` falls within the valid range for both the `feeCycle` and `burnFees` arrays. By doing this, you ensure that `_index` is checked against the lengths of both arrays, thereby preventing potential errors and maintaining the integrity of the staking configuration process. Additionally, consider discussing with the project owners if the restriction on changing the burn fee after the last cycle is an intentional strategic decision to ensure it aligns with the project’s goals. 

**Fix** - The client made an attempt to address the issue in commit e935e74.  Mean while the issue in itself is addressed the change proposed introduced significant bugs that undermines the functionality of the codebase. 

### Centralization Risk Due to Privilege Management of updateLastInvestTimestamp Method

**Severity**: Low

**Status**: Acknowledged

**Location**: PoolBase.sol

**Description**

The current implementation of the `updateLastInvestTimestamp` function allows not only the contract owner but also any wallet listed in the `presales` mapping to update the last investment timestamp for users. This creates a centralization risk wherein the power to modify critical user-specific parameters. If any of these privileged wallets (owner or presales) are compromised, it could lead to unauthorized and potentially malicious modifications to user data. This design undermines the principles of decentralization and poses a significant concern to the contract and its users.

**Recommendation**: 

To mitigate this centralization risk, it is recommended to implement a multisignature (`multisig`) scheme for both the owner and even the presales wallets. This approach would require multiple approvals for any privileged action, significantly reducing the likelihood of unauthorized changes even if one of the wallets is compromised. Additionally, the contract can be enhanced to include further checks and balances, such as time-lock mechanisms or community voting for critical updates, to ensure that user-specific parameters are modified transparently and securely. 

**Fix**:  The client recognized the risk and mentioned that a multisig scheme is being adopted to substitute the admin's wallet in functions with elevated privileges.

## Informational

### Rewards Not Compounded For Users with Non-Zero lockedUntil 

**Severity** - Informational

**Status** - Acknowledged

**Description**

The pool contract only rewards stakers with yield who staked without a lock period (for these stakers `liquidWeight` is being assigned , for users who have locked their stake for a duration there `liquidWeight` is 0) inside `_unstake` (L655-L656) . Though this may be a design choice it seems that stakers who actually staked for a duration and locked their stake don’t receive accrued yield.

**Recommendation**:

The design choice should be acknowledged.

### Hardcoded Address in `_unstake` Function

**Severity** - Informational

**Status** - Acknowledged

**Location** - PoolBase.sol

**Description**

The `_unstake` function in the `PoolBase` contract contains a hardcoded address for burning tokens. Hardcoding addresses can lead to issues when deploying the contract on different networks or when the address needs to change, as it requires recompilation and redeployment of the contract.

**Location**: The hardcoded address is found in the `_unstake` function:

``address(0x000000000000000000000000000000000000dEaD)``

**Recommendation**: 

To avoid hardcoding addresses, declare the burn address as an `immutable` variable and initialize it via the constructor. This approach allows the address to be set at deployment time and remain unchanged thereafter, ensuring flexibility across different deployments.

**Proposed Solution**:

- Add an `immutable` variable for the burn address.
- Initialize the burn address in the constructor.

```solidity
address public immutable burnAddress;
constructor(
    address _buidl,
    IPoolFactory _factory,
    address _poolToken,
    uint64 _initBlock,
    uint32 _weight,
    address _starterInfo,
    address _burnAddress
) {
    require(address(_factory) != address(0), "-2");
    require(_poolToken != address(0), "-3");
    require(_initBlock != 0, "-4");
    require(_weight != 0, "-5");
    require(_starterInfo != address(0), "-6");
    require(_burnAddress != address(0), "-21");
    starterInfo = IStarterInfo(_starterInfo);
    buidl = _buidl;
    factory = _factory;
    poolToken = _poolToken;
    lastYieldDistribution = _initBlock;
    weight = _weight;
    burnAddress = _burnAddress;
}
```

### Use of require Statements Instead of Modifiers for Access Control

**Severity**: Informational

**Status**: Resolved

**Location**: CorePool.sol & PoolBase.sol

**Description**

The `CorePool` contract uses `require` statements for access control in several functions. This approach reduces readability and maintainability. Instead, Solidity modifiers should be used to enforce access control, making the code cleaner and more understandable.

The `PoolBase` contract lacks the use of modifiers for access control in several functions (`stakeFor`, `setWeight`, `clearHistory`, `setConfiguration`, `setInitialSettings`). While `require` statements are used to enforce certain conditions, they do not provide the same level of readability and maintainability as dedicated access control modifiers. 

**Affected Code**

The following require statements are used for access control:

- Factory Owner Check:
``require(factory.owner() == msg.sender, "-1");``

- Vault Address Check:
``require(msg.sender == vault, "-3");``

- Pool Existence Check:
``require(factory.poolExists(msg.sender), "-5");``

- Function `stakeFor`:
```solidity
function stakeFor(
    address _staker,
    uint256 _amount,
    uint64 _lockUntil
) external override {
    require(_staker!= msg.sender, "-7");
    _stake(_staker, _amount, _lockUntil, false, 0);
    history.push(_staker);
}
```

- Function `setWeight`:
```solidity
function setWeight(uint32 _weight) external override {
    require(msg.sender == address(factory), "-8");
    emit PoolWeightUpdated(msg.sender, weight, _weight);
    weight = _weight;
}
```

- Function `clearHistory`:
```solidity
function clearHistory() external {
    require(msg.sender == factory.owner(), "-18");
    delete history;
}
```
- Function `setConfiguration`:
```solidity
function setConfiguration(
    uint256 _rewardPerWeightMultiplier,
    uint256 _yearStakeWeightMultiplier,
    uint256 _weightMultiplier,
    uint256 _liquidMultiplier
) external {
    require(msg.sender == factory.owner(), "-19");
    emit PoolConfigurationUpdated(...);
   ...
}
```
- Function `setInitialSettings`:
```solidity
function setInitialSettings(address _factory, address _poolToken) external {
    require(msg.sender == factory.owner(), "-20");
    factory = IPoolFactory(_factory);
    poolToken = _poolToken;
}
```

**Recommendation**: 

Define and use modifiers for these access control checks. This will improve code readability and maintainability.

```solidity
modifier onlyFactoryOwner() {
    require(factory.owner() == msg.sender, "-1");
    _;
}
modifier onlyVault() {
    require(msg.sender == vault, "-3");
    _;
}
modifier onlyExistingPool() {
    require(factory.poolExists(msg.sender), "-5");
    _;
}
modifier onlyFactory() {
    require(msg.sender == address(factory), "Caller is not the factory");
    _;
}
```

### Redundant Initialization of Variables

**Severity**: Informational

**Status**: Resolved

**Location**: PoolBase.sol


**Description**

The code contains redundant initializations of variables to zero. In Solidity, the default value for uninitialized variables is zero, making these initializations unnecessary. Removing these redundant initializations can improve code readability and slightly reduce gas costs.

**Location**:

The for loop in the getTokenBurnFee function:
```solidity
for (uint256 i = 0; i < feeCycle.length; i++) {
The totalSum variable in the getRewardLockPeriod function:
uint256 totalSum = 0;
```

**Recommendation**: 

Remove the redundant initializations of variables to zero. This will make the code cleaner and more efficient.


