**Auditors**

[Zokyo](https://x.com/zokyo_io)

# Findings

## High Risk

### The referrer receives the lowest available amount for the first purchase, even when he referred above the maximum threshold, leading to receiving fewer funds than expected.

**Severity**: Critical

**Status**: Acknowledged 

**Description**

When a user purchases, let's say, 10 vaults using a referrer's referral code, the number of referrals for the referrer increases by 10. This number is used to calculate the percentage of the paid funds that the referrer receives for the purchase of the 10 vaults. However, since the number of referrals is updated only after completing the payment, the first purchase always counts the referrer's referrals as 0. Therefore, even if the user is buying 1,000 vaults, the referrals will be 0 instead of 1,000 as supposed. This leads to the referrer receiving the least available amount of funds, as they are accounted for as having 0 referrals.

**Proof of concept (POC)**:

```solidity
function testPOCReferrerIsNotGettingPaidForReferrals() public {
       vm.startPrank(randomUser);
      
       (uint256 price, ) = lumiaVault.pricePerVault();
       paymentToken.dispense();
       uint256 numOfBuyingVaults = 1000;
       paymentToken.approve(address(lumiaVault), price * numOfBuyingVaults);

       string memory referralCode = lumiaVault.getReferralCode(lumiaWalletAddress);

       // User buys with lumia referral code buy it counts 0 referrals
       uint256 referrerBalanceBefore = paymentToken.balanceOf(lumiaWalletAddress);
       lumiaVault.purchaseFirstVaults(referralCode, numOfBuyingVaults);
       uint256 referrerBalanceAfter = paymentToken.balanceOf(lumiaWalletAddress);
       uint256 referrerAmount = ( price * numOfBuyingVaults * 5) / 100;
       assert(referrerBalanceAfter == referrerBalanceBefore + referrerAmount);

       // User buys again with lumia referral code and now it counts 1000 referrals
       paymentToken.approve(address(lumiaVault), price);
       uint256 secondBuyAmount = 1;
       uint256 referrerBalanceBefore2 = paymentToken.balanceOf(lumiaWalletAddress);
       lumiaVault.purchaseMoreVaults(secondBuyAmount);
       uint256 referrerBalanceAfter2 = paymentToken.balanceOf(lumiaWalletAddress);
       uint256 referrerAmount2 = ( price * secondBuyAmount * 10) / 100;
       assert(referrerBalanceAfter2 == referrerBalanceBefore2 + referrerAmount2);
       vm.stopPrank();
   }

```

**Recommendation**:

Change the order in `purchaseVaults` function. First call `updateReferrals`, then call `sendPayment` and then call `addVaults`.

**Client comment**: We know this, it is written this way by design. Acknowledged then, the referrer receives 0 if there is only 1 buy with its referral code.


### Users can purchase vaults by paying less amount of funds than expected, even by the least

**Severity**: Critical	

**Status**: Acknowledged 

**Description**

Users call to `purchaseFirstVaults()` or `purchaseMoreVaults()` in `NodeSale.sol` in order to buy vaults. These functions receive `numberOfVaults` as parameter to indicate the amount of vaults that a user wants to purchase. 
The price for the vaults increases once a certain threshold of sold vaults is reached. However, when a user starts a purchase transaction of X vaults, the amount of funds needed to be paid is calculated based on the current number of sold vaults, without considering the attainment of the mentioned thresholds that increase the vault's price.

Consider the following scenario:

- The price for the first 10000 vaults is 1000 USDB.
- Different users purchase 9999 vaults.
- Now there is still only 1 vault left for 1000 USDB, the number 10001 will increase in price increase.
- A new user buys 1000 vaults. The price for each of these vaults will be calculated with the price of vault number 9999. So the user will pay 1000 USDB * 1000 vaults. The user is actually paying less funds that expected due to the thresholds.

**Proof of Concept (POC)**:

```solidity
function testPOCByPassIncreasePrice() public {
       vm.startPrank(randomUser);
      
       (uint256 price, uint256 amountUntilNextPrice) = lumiaVault.pricePerVault();
       paymentToken.dispense();
       paymentToken.approve(address(lumiaVault), price * (amountUntilNextPrice - 1));

       string memory referralCode = lumiaVault.getReferralCode(lumiaWalletAddress);
       uint256 numOfBuyingVaults = 1;

       lumiaVault.purchaseFirstVaults(referralCode, amountUntilNextPrice - 1);

       (uint256 price2, uint256 amountUntilNextPrice2) = lumiaVault.pricePerVault();

       uint256 balanceBefore = paymentToken.balanceOf(randomUser);
       uint256 numbreOfBuyingVaults = 5;
       paymentToken.approve(address(lumiaVault), price * numbreOfBuyingVaults);
       lumiaVault.purchaseMoreVaults(numbreOfBuyingVaults);
       uint256 balanceAfter = paymentToken.balanceOf(randomUser);

       assert(balanceAfter == (balanceBefore - (numbreOfBuyingVaults * price2)));
       vm.stopPrank();
   }
```


It is even possible that a user buy every vault in 1 transaction so that the price for every vault will be calculated as the cheapest price.

- **Proof of concept (POC)**:

```solidity
function testPOCBuyAllVaultsByMinPrice() public {
      uint256 MAX_ALLOWED_VAULTS = 50000;

       vm.startPrank(randomUser);
      
       (uint256 price, uint256 amountUntilNextPrice) = lumiaVault.pricePerVault();
       paymentToken.dispense();
       paymentToken.approve(address(lumiaVault), price * MAX_ALLOWED_VAULTS);

       string memory referralCode = lumiaVault.getReferralCode(lumiaWalletAddress);

       uint256 balanceBefore = paymentToken.balanceOf(randomUser);
       lumiaVault.purchaseFirstVaults(referralCode, MAX_ALLOWED_VAULTS);
       uint256 balanceAfter = paymentToken.balanceOf(randomUser);

       assert(balanceAfter == (balanceBefore - (price * MAX_ALLOWED_VAULTS)));
   }
```

	
**Recommendation**:

The calculation of `totalAmount` (uint256 totalAmount = numberOfVaults * getPricePerVault();) has to consider if the `numberOfVaults` is crossing the next threshold and sum up the new price for the vaults after the threshold. Consider creating a new function for this purpose.
**Client comment**: We know this, it is written this way by design. Acknowledged because user can pay less funds than the correct amount

### Centralization Risk Due to Overpowered Owner

**Severity**: Critical

**Status**: Acknowledged

**Description**

An admin calls `setNumberOfVaults(accountAddress, value)` to set the number of vaults for an account to any desired amount without any payment. This can be done without any checks for the `MAX_ALLOWED_VAULTS` constraint.  If an attacker gains control of the admin account, they can allocate themselves an excessive number of vaults without any payment, leading to a significant loss of funds and trust. An admin can use `addMultipleVaults(accountAddress, numberOfVaults)` to increase the number of vaults for any account without requiring any funds. This can lead to an inflation of the total number of vaults beyond the intended cap, disrupting the vault pricing mechanism and the overall supply.

**Recommendation**: 
Use a multi-signature wallet for executing onlyOwner functions. This requires multiple authorized signatures to approve critical actions, reducing the risk of a single point of failure. 
**Client comment**: A wallet with admin authorization should be able to do this. It is not our responsibility whether they ensure wallet security or not.

### User can get front-run when purchasing a vault leading to the referrer receiving nothing.

**Severity**: High	

**Status**: Acknowledged

**Description**

If a user, lets say user A, calls `purchaseFirstVaults()` with user B’s referral code and the admin notices the transaction, he could front-run it while it is in the mempool and call `setUniqRefferralCode()` setting their own referral code for User B before the original transaction is mined. When User B’s transaction is processed, the createNewAccount function checks if a referral code already exists and uses the admin’s newly set code.
As a result, User A gets zero referrals, and the admin receives all the referral rewards from User B’s purchase.
This is not only possible with `purchaseFirstVaults()` but also with `purchaseMoreVaults()` for an account that already has been created with a valid referral code.

**Recommendation**:

There are several different ways to mitigate this issue:
- Do not allow an admin to set user’s referral codes by calling `setUniqRefferralCode()`.
- Modify `createNewAccount` function to not check if it has already a referral code but use the new one.
- Make referrals immutable (this would only mitigate the case of front-running and changing a referral but no the rest).
- Add a time lock mechanism for admin to change referral codes.
- Add a 2 step mechanism for changing a user’s referral code where the user must accept the change as the second step.
**Client comment**: The administrator must have these authorisations. It is not our responsibility if the admin loses the private key. Also I change `setUniqRefferralCode` function, can you check it one more time

### Users may end up paying more than expected due to uncontrolled slippage.

**Severity**: High	

**Status**: Resolved

**Description**

When a user purchases x amount of vaults by calling `purchaseFirstVaults()` or `purchaseMoreVaults()` the total amount of funds needed to be paid is calculated depending on the vault id. There are certain defined thresholds of sold vaults that increase the price. If the amount of vaults purchased exceeds a threshold the user will pay more funds than expected.

**Consider this scenario**:
- User approves uint256.max tokens to spend.
- vaultCount is 9999 and vault’s price changes at 10000.
- User purchases 100 vaults.
- The funds paid are going to be more than expected due to reaching the threshold.

**Recommendation**:

Add an uint256 `maxAmount` parameter in the function which represents the maximum amount of tokens a user is willing to pay. Once the amount that is needed to be paid is calculated, compare it with `maxAmount`, if it is greater, revert.


### Users can get front-run and as a consequence pay more funds than expected

**Severity**: High	

**Status**: Resolved

**Description**

There are certain defined thresholds of sold vaults that increase the vault’s price. When a user submits a transaction for buying x amount of vaults, he can get front-run by other user that buys y amount of vaults moving `vaultCount` above the next threshold increasing the vault’s price. As a consequence the user buying x amount of vaults will pay more funds than expected.

**Recommendation**:

Add an uint256 `maxAmount` parameter in the function which represents the maximum amount of tokens a user is willing to pay. Once the amount that is needed to be paid is calculated, compare it with `maxAmount`, if it is greater, revert.

### `vaultCount` can be wrongly tracked leading to an incorrect vault price calculation.

**Severity**: High	

**Status**: Acknowledged

**Description**

The functions `getPricePerVault()` and `getPricePerVaultForID()` are used for calculating the current vault’s price. These functions uses `vaultCount`, a variable that is increased each time a user buys a new vault, to consider the number of already sold vaults and to calculate the current price of the next vault. When a user buys vaults by calling `purchaseFirstVaults()` or `purchaseMoreVaults()` `vaultCount` is increased but if the admin calls `setNumberOfVaults()` and increases/decreases the number of vaults for a user `vaultCount` is not modified.

**Consider the following scenario**:
- 10k vaults are sold.
- `setNumberOfVaults()` is executed giving 5k vaults to a user.
- The actual number of vaults now is 15k but vaultCount is still 10k.

If a new user buys now a vault, the price of the vault will be calculated as if it were the 10.001 vault instead of being the 15.001.

**Recommendation**:

Update `vaultCount` every time `setNumberOfVaults()` is called for increasing or decreasing the number of vaults for a user.

## Medium Risk

### The same referral code can be set for 2 different addresses.

**Severity**: Medium	

**Status**: Resolved

**Description**

The `setUniqReferralCode()` function allows the `ADMIN_ROLE` to set a specific referral code to an address. There are not checks to ensure that the set code is not already being used to another address. This can lead to an scenario where a referrer loses their referees because its code has been unintentionally set to another account.

**Proof of concept**:
```solidity
function testPOCReeplaceAlreadyUsedCODE() public {
       vm.startPrank(deployer);
  
       string memory referralCode = lumiaVault.getReferralCode(lumiaWalletAddress);
       address referrerAddress = lumiaVault.codeToAddress(referralCode);
       assert(lumiaWalletAddress == referrerAddress);

       // Set the same code to other address
       lumiaVault.setUniqReferralCode(randomUser, referralCode);
       address referrerAddressAfter = lumiaVault.codeToAddress(referralCode);
       assert(referrerAddressAfter == randomUser);

       vm.stopPrank();
   }
```

**Recommendation**:

- Add a check to ensure that the code is not already being used:
``require(codeToAddress[code] == address(0));``


### MasterAccounts are not able to buy vaults.

**Severity**: Medium	

**Status**: Acknowledged

**Description**

When `NodeSale.sol` is deployed 2 calls to `createMasterAccount` are executed within the contract constructor. These calls creates 2 accounts without the need of a referred. The addresses created as `master accounts` without a referrer are not allowed to buy vaults because `purchaseFirstVaults()` checks if `accountData[accountAddress].accountId == 0`which is not the case and `purchaseMoreVaults` implements the following `require`:

```solidity
require(referrer != address(0), "LumiaVault: Invalid Referral Code");
```


As the accounts created as `master accounts` did not need a referred then this check will neither be passed.

**Proof of Concept (POC)**:
```solidity
function testMasterAccountBuysVault() public {
       vm.startPrank(partnerWalletAddress);
       (uint256 price, ) = lumiaVault.pricePerVault();
       paymentToken.dispense();
       paymentToken.approve(address(lumiaVault), price);

       string memory referralCode = lumiaVault.getReferralCode(lumiaWalletAddress);
       uint256 numOfBuyingVaults = 1;
       vm.expectRevert();
       lumiaVault.purchaseFirstVaults(referralCode, numOfBuyingVaults);

       vm.expectRevert();
       lumiaVault.purchaseMoreVaults(1);
      
       vm.stopPrank();
   }
```


**Recommendation**:

Consider adding an invented for the purpose referred for these accounts so that `referrer != address(0)`.
**Client comment**: it’s by design.

### The maximum number of 50000 vaults invariant can be bypassed and lead to DOS

**Severity**: Medium	

**Status**: Acknowledged

**Description**

There is an invariant in the business logic which sets the maximum number of available vaults to 50000. This invariant is checked in the `addVaults` function when a user purchases a new set of vault to ensure that the total amount of vaults is less or equal to 50000. However the `DEFAULT_ADMIN_ROLE` can bypass this invariant by calling `setNumberOfVaults` which sets X amount of vaults to an account without any check. This leads to a DOS as no vaults can be bought by the users.

**Recommendation**:

Track the increase of number of vaults when calling `setNumberOfVaults` and a check to ensure that vaultCount is still less or equal to 50000.

### Transfer of native ETH may fail.

**Severity**: Medium

**Status**: Resolved

**Description**

The `retrieveNative()` function in the `RetrieveAsset.sol` contract uses the native `transfer()` function to transfer ETH. `transfer()` only provides 2300 gas for its operation. This means the following cases can cause the transfer to fail: 

- The contract's callback spends more than 2300 gas (which is only enough to emit something) 

- The contract is called through a proxy which itself uses up the 2300 gas 

- Complex operations or external calls

If a user falls into one of the above categories, they'll be unable to receive funds from the contract.

**Recommendation**: 

Consider using the low-level .call() instead of transfer() and check the returned value to ensure that the transfer of ether has been correctly completed.

## Low Risk

### Lack of Event Emission for Critical Functions

**Severity**: Low

**Status**: Resolved

**Description**: 

Functions that change important state variables or have significant effects (like `setNumberOfVaults`, `addMultipleVaults`) do not emit events. This can make tracking changes difficult and reduce transparency.

**Scenario**:

- An admin calls `setNumberOfVaults` to set the number of vaults for an account.
- No event is emitted, making it harder for external observers to track this critical change.

**Recommendation**: 

Emit events in functions that modify critical state variables to improve transparency


### Transfers can fail silently.

**Severity**: Low

**Status**: Resolved

**Description**: 

The `RetrieveAsset.sol` contract executes several `transfer` and `transferFrom` calls without checking the return value and certain tokens transfer can fail without reverting. It is advisable to include a `require()` statement to verify the return value of token transfers or to use OpenZeppelin's `safeTransfer/safeTransferFrom`. Neglecting this can result in silent transfer failures and disrupt token accounting within the contract.

**Recommendation**:

Use `safeTransferFrom` and `safeTransfer` instead. For the case of ETH transfer use `.call()` and add a `require` for checking the returned value.

## Informational

### Unused Variables and Functions

**Severity**: Informational

**Status**: Resolved

**Description**: 


The contract contains variables and functions that are declared but never used, which can confuse readers and auditors.
`uint256 public lastPrice;`

**Recommendation**: 
Remove any unused variables and functions to clean up the contract.


### Use of constant and immutable Keywords

**Severity**: Informational 

**Status**: Resolved

**Description**: 


The contract can benefit from the use of constant and immutable keywords for variables that do not change after deployment. This can save gas costs by storing the variables directly in the bytecode

**Recommendation**: Mark variables that do not change after deployment as constant or immutable.
```solidity
address public immutable lumiaAddress; 
address public immutable partnerAddress;
address public immutable providerAddress;
```

### Unusable `retrieve721()` function because contract can not receive ERC721 tokens.

**Severity**: Informational

**Status**: Resolved

**Description**: 

The function `retrieve721()` is used to transfer a ERC721 token from the contract itself to the caller but the contract is not ERC721Receiver so it will not be able to receive any ERC721 token.

**Recommendation** : 

To enable the contract to receive and manage ERC721 tokens, implement the  `ERC721Receiver` interface in the contract. This involves adding the `onERC721Received` function, which allows the contract to accept ERC721 tokens.

### Unusable `retrieveNative()` function because contract can not receive ETH.

**Severity**: Informational

**Status**: Resolved

**Description**: 

The function `retrieveNative()` is used to transfer ETH from the contract itself to the caller but the contract does not implement a `receive()` function and neither a payable one.

**Recommendation**: 

To enable the contract to receive and manage ETH, add a `receive()` function or a payable fallback function to the contract. This allows the contract to accept ETH.



